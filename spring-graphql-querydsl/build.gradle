plugins {
	id 'org.springframework.boot' version '2.7.0-RC1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'com.github.ben-manes.versions' version '0.42.0'
	id 'org.sonarqube'
	id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '18'
targetCompatibility = '18'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	maven { url 'https://repo.spring.io/milestone' }
}

ext {
	set('testcontainersVersion', "1.17.1")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-graphql'

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.liquibase:liquibase-core'
	// Printing Queries effectively
	implementation 'net.ttddyy:datasource-proxy:1.7'

	// QueryDSL for JPA
	implementation 'com.querydsl:querydsl-core'
	implementation 'com.querydsl:querydsl-jpa'

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok',
            'com.querydsl:querydsl-apt:5.0.0:jpa',
			'org.hibernate.javax.persistence:hibernate-jpa-2.1-api:1.0.2',
            'javax.annotation:javax.annotation-api'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testImplementation 'org.springframework.graphql:spring-graphql-test'
	testImplementation 'org.projectlombok:lombok'
	// WebGraphQlTester needs webflux as dependency 
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
}

dependencyManagement {
	imports {
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
	}
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'build/generated/sources/annotationProcessor/java/main']
        }
    }
}

defaultTasks "bootRun"

springBoot {
	buildInfo()
}

compileJava.dependsOn processResources
processResources.dependsOn bootBuildInfo

test {
	useJUnitPlatform()
}

file("sonar-project.properties").withReader {
	Properties sonarProperties = new Properties()
	sonarProperties.load(it)

	sonarProperties.forEach { key, value ->
		sonarqube {
			properties {
				property key as String, value
			}
		}
	}
}
